
&НаКлиенте
Процедура ВывестиОтчет(Команда)
	ВывестиОтчетНаСервере().Показать();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВывестиОтчетНаСервере()
	
	СхемаКомпоновкиДанных = Отчеты.ОстаткиТоваровНаСкладах.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	НастройкиКомпоновки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию; 
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ТабДок = Новый ТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабДок);

	ПроцессорВывода.НачатьВывод();

	ПризнакФиксации = Ложь;
	Пока Истина Цикл
		
		ЭлементРезультата = ПроцессорКомпоновки.Следующий();
		Если ЭлементРезультата = Неопределено Тогда
			Прервать;
		Иначе
			Если Не ПризнакФиксации И ЭлементРезультата.ЗначенияПараметров.Количество() > 0 Тогда
				ПризнакФиксации = Истина;
					
				ТабДок.ФиксацияСверху = ТабДок.ВысотаТаблицы;		
			КонецЕсли;	
				 
			ПроцессорВывода.ВывестиЭлемент(ЭлементРезультата);
		КонецЕсли;	 
	 
	КонецЦикла;

	ПроцессорВывода.ЗакончитьВывод();
	
	Возврат ТабДок
	
КонецФункции

&НаКлиенте
Процедура СформироватьКоллекцию(Команда)
	СформироватьКоллекциюНаСервере();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СформироватьКоллекциюНаСервере()
	
	СхемаКомпоновкиДанных = Отчеты.ОтчетПоОстаткам.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	НастройкиКомпоновки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию; 
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ТЗ = Новый ТаблицаЗначений;	

	ПроцессорВывода.УстановитьОбъект(ТЗ);

	ПроцессорВывода.Вывести(ПроцессорКомпоновки);

КонецПроцедуры

